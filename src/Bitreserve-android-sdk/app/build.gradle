apply plugin: 'com.android.library'

android {
    compileSdkVersion 22
    buildToolsVersion "22.0.1"
    publishNonDefault true
    testBuildType "debug"

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }

    defaultConfig {
        minSdkVersion 10
        targetSdkVersion 22
        versionCode 1
        versionName "1.0"
        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
    }

    lintOptions {
        abortOnError false
    }

    packagingOptions {
        exclude 'LICENSE.txt'
    }

    testOptions {
        unitTests.returnDefaultValues = true
    }

    productFlavors {
        production {
            buildConfigField 'String', 'API_SERVER_URL', '"https://api.bitreserve.org"'
            buildConfigField 'String', 'AUTHORIZATION_SERVER_URL', '"https://bitreserve.org"'
            buildConfigField 'Boolean', 'IS_DEBUG_ENABLE', 'false'
        }

        sandbox {
            buildConfigField 'String', 'API_SERVER_URL', '"https://api-sandbox.bitreserve.org"'
            buildConfigField 'String', 'AUTHORIZATION_SERVER_URL', '"https://sandbox.bitreserve.org"'
            buildConfigField 'Boolean', 'IS_DEBUG_ENABLE', 'true'
        }
    }
}

dependencies {
    compile fileTree(dir: 'libs', include: ['*.jar'])
    compile 'com.darylteo:rxjava-promises-java:1.2.0'
    compile 'com.squareup.retrofit:retrofit:1.9.0'
    compile 'com.squareup.okhttp:okhttp:2.0.0'
    compile 'com.squareup.okhttp:okhttp-urlconnection:2.0.0'
    androidTestCompile 'com.android.support.test:testing-support-lib:0.1'
    androidTestCompile 'org.hamcrest:hamcrest-library:1.1'
    androidTestCompile ('com.github.javafaker:javafaker:0.5')
    {
        exclude group: 'org.yaml' //The version supported by android is at libs folder
    }
    androidTestCompile files ('libs/snakeyaml-1.10-android.jar')
    testCompile 'junit:junit:4.12'
    testCompile 'org.powermock:powermock-api-mockito:1.6.2'
    testCompile 'org.powermock:powermock-module-junit4:1.6.2'
}

task JavadocGeneration(type: Javadoc) {
    source = android.sourceSets.main.java.srcDirs
    classpath += project.files(android.getBootClasspath().join(File.pathSeparator))
    destinationDir = file("../javadoc/")
    failOnError false
}
